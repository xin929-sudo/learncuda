#include<stdio.h>
#include<cuda_runtime.h>
#include<limits.h>
#include<cooperative_groups.h>

namespace cg = cooperative_groups;
// å®å®šä¹‰ï¼šåŒ…è£¹å‡½æ•°è°ƒç”¨ï¼Œæ£€æŸ¥è¿”å›å€¼
#define CUDA_CHECK(call) \
do { \
    const cudaError_t error = call; \
    if (error != cudaSuccess) { \
        fprintf(stderr, "Error: %s:%d, ", __FILE__, __LINE__); \
        fprintf(stderr, "code: %d, reason: %s\n", error, cudaGetErrorString(error)); \
        exit(1); \
    } \
} while (0)

// cpu version matrix matmul 
// A is (M,K), B is (K,N)
void matmulCPU(float* A,float* B,float* C,int M,int N,int K) {
    for(int i = 0; i < M; ++i) {
        for(int j = 0; j < N; ++j) {
            float sum = 0.0f;
            for(int k = 0; k < K; k++) {
                sum += A[i * K + k] * B[k * N + j];
                    // A[i][k] * B[k][j]
            }
            C[i * N + j] = sum;
        }
    }
}

// gpu version :navive matmul
__global__ void matmul_native(float* A,float* B,float* C,int M,int N,int K) {

    // è®¡ç®—çº¿ç¨‹è´Ÿè´£çš„Cå…ƒç´ ä½ç½®
    int row = blockIdx.y * blockDim.y + threadIdx.y;
    int col = blockIdx.x * blockDim.x + threadIdx.x;

    // è¾¹ç•Œæ£€æŸ¥
    if(row < M && col < N) {
        float sum = 0.0f;

        // è®¡ç®—ç‚¹ç§¯ï¼šAçš„row è¡Œï¼Œä¹˜ä»¥ B çš„ col åˆ—
        for(int k = 0; k < K; ++k){
            sum += A[row * K + k] * B[k * N + col];
                // A[row][k] * B[k][col]
        }
            // å†™å›ç»“æœ
        C[row * N + col] = sum; 
    }
}
// gpu version: tile matmul
#define TILE_SIZE 16
__global__ void matmalGPU_tiled(float* A,float* B,float* C,int M,int N,int K) {
    int row = blockIdx.y * blockDim.y + threadIdx.y;
    int col = blockIdx.x * blockDim.x + threadIdx.x;
    int tx = threadIdx.x;
    int ty = threadIdx.y;
    // å®šä¹‰shared memory
    __shared__ float As[TILE_SIZE][TILE_SIZE];
    __shared__ float Bs[TILE_SIZE][TILE_SIZE];

    float sum = 0.0f;
    int numTiles = (K + TILE_SIZE - 1) / TILE_SIZE; // è¿›è¡Œåˆ’åˆ†ï¼Œå› ä¸ºä¸€æ¬¡è£…ä¸äº†è¿™ä¹ˆå¤šæ•°æ®

    for(int t = 0; t < numTiles; ++t){
        // load A B to shared memory
         int aCol = t * TILE_SIZE + tx;
         if(row < M && aCol < K) {
            As[ty][tx] = A[row * K + aCol];
         } else {
            As[ty][tx] = 0.0f;
         }

         int bRow = t * TILE_SIZE + ty;
         if(col < N && bRow < K) {
            Bs[ty][tx] = B[bRow * N + col];
         } else {
            Bs[ty][tx] = 0.0f;
         }
         // ç­‰å¾…æ‰€æœ‰æ•°æ®
         __syncthreads();
         // ä½¿ç”¨ shared memory  to calculate tiles value
         for(int k = 0; k < TILE_SIZE; ++k) {
            sum += As[ty][k] * Bs[k][tx];
         }

         __syncthreads();
    }

    // write back
    if(row < M && col < N) {
        C[row * N + col] = sum;
    }
}
// æ¯ä¸ªçº¿ç¨‹éœ€è¦è®¡ç®— 4*4 ä¸ªæ•°æ®
__global__ void matmalGPU_tiled4(float* A,float* B,float* C,int M,int N,int K) {
    
    cg::thread_block block = cg::this_thread_block();

    // è®¡ç®— å½“å‰çº¿ç¨‹å—è´Ÿè´£çš„CçŸ©é˜µçš„èµ·å§‹ä½ç½®
    int blockRow = blockIdx.y * TILE_SIZE * 4;
    int blockCol = blockIdx.x * TILE_SIZE * 4;
    // è®¡ç®— å½“å‰çº¿ç¨‹åœ¨çº¿ç¨‹å—å†…çš„åæ ‡
    int ty = threadIdx.y;
    int tx = threadIdx.x;
    // å®šä¹‰shared memoryï¼Œæ³¨æ„è¿™é‡Œçš„å°ºå¯¸éœ€è¦è°ƒæ•´ï¼Œå› ä¸ºæ¯ä¸ªçº¿ç¨‹è¦å¤„ç†4*4çš„æ•°æ®
    // æ ¹æ® C = A * B çš„è®¡ç®—æ–¹å¼ï¼ŒAçš„tileéœ€è¦æ˜¯ 64*Kï¼ŒBçš„tileéœ€è¦æ˜¯ K*64ï¼Œ
    // AçŸ©é˜µéœ€è¦æä¾›64è¡Œæ•°æ®ï¼ŒBçŸ©é˜µéœ€è¦æä¾›64åˆ—æ•°æ®
    // Kçš„ç»´åº¦æˆ‘ä»¬ä¸€æ¬¡è£…ä¸ä¸‹ï¼Œæ‰€ä»¥éœ€è¦åˆ†å—åŠ è½½
    __shared__ float As[TILE_SIZE * 4][TILE_SIZE];
    __shared__ float Bs[TILE_SIZE][TILE_SIZE * 4];
   // è¿›è¡Œåˆ’åˆ†ï¼Œå› ä¸ºä¸€æ¬¡è£…ä¸äº†è¿™ä¹ˆå¤šæ•°æ®
    int numTiles = (K + TILE_SIZE - 1) / TILE_SIZE; // å‘ä¸Šå–æ•´
    float sum[4][4] = {{0.0f}};
    for(int t = 0; t < numTiles; ++t){
        // load A B to shared memory
        // tile_k æ˜¯å½“å‰å—åŠ è½½çš„Aå’ŒBçš„èµ·å§‹åˆ—/
        // int tile_k = t * numTiles;

        for(int i = 0; i < 4; ++i) {
                        // å¤§å—çš„èµ·å§‹è¡Œï¼Œå½“å‰çº¿ç¨‹è¦å¤„ç†çš„4ä¸ªæ•°æ®ï¼Œiæ˜¯åç§»é‡
            int aRow = blockRow + ty * 4 + i; // Açš„è¡Œè·Ÿ Cçš„è¡Œæœ‰å…³
            int aCol = t*TILE_SIZE + tx;      // Açš„åˆ—è·Ÿ Kæœ‰å…³ï¼Œè€Œè·ŸCçš„è¡Œæ— å…³     
            As[4 * ty + i][tx] = (aRow < M && aCol < K) ? (A[aRow * K + aCol]) : 0.0f;
                        // tile_kå¯¹åº”AçŸ©é˜µçš„åˆ—
           
        }
        for(int j = 0; j < 4; ++j) {
            // Bs æ˜¯ 16 * 64 
                    // å¤§å—çš„èµ·å§‹è¡Œï¼Œå½“å‰çº¿ç¨‹è¦å¤„ç†çš„4ä¸ªæ•°æ®ï¼Œiæ˜¯åç§»é‡
            int bRow = t * TILE_SIZE + ty; // B çš„è¡Œ è·Ÿ Cæ— å…³ï¼Œåªè·Ÿ K æœ‰å…³
            int bCol = blockCol + tx * 4 + j; // Bçš„åˆ— è·Ÿ Cæœ‰å…³
                    // tile_kå¯¹åº”AçŸ©é˜µçš„åˆ—
            Bs[ty][4 * tx + j] = (bRow < K && bCol < N) ? B[bRow * N + bCol] : 0.0f;
            // As æ˜¯ 64 * 16ï¼Œä¸€ä¸ªblocké‡Œé¢ æœ‰ 16 * 16 ä¸ªçº¿ç¨‹ï¼Œåˆ—å¥½å¤„ç†
            
        }
        // ç­‰å¾…çº¿ç¨‹å—è¯»å–
        block.sync();
        
        for(int k = 0; k < TILE_SIZE; ++k) {
            float a_reg[4],b_reg[4];
            // æ¯ä¸ªçº¿ç¨‹éœ€è¦è®¡ç®—4*4ä¸ªæ•°æ®ï¼Œæ‰€ä»¥éœ€è¦æŠŠAçš„4è¡Œå’ŒBçš„4åˆ—åŠ è½½åˆ°å¯„å­˜å™¨
            for(int i = 0; i < 4; ++i) {
                a_reg[i] = As[ty *4 + i][k];
            }
            for(int j = 0; j < 4; ++j) {
                b_reg[j] = Bs[k][tx * 4 + j];
            }
            for(int i = 0; i < 4; ++i){
                for(int j = 0; j < 4 ;j++){
                    sum[i][j] += a_reg[i] * b_reg[j];
                }
            }
        }
        block.sync();
    }
    // write back
    for(int i = 0; i < 4; ++i) {
        for(int j = 0; j < 4; ++j) {
            // è®¡ç®—å½“å‰çº¿ç¨‹è´Ÿè´£çš„Cå…ƒç´ ä½ç½®
            // å¤§å—çš„èµ·å§‹è¡Œï¼Œå½“å‰çº¿ç¨‹è¦å¤„ç†çš„4ä¸ªæ•°æ®ï¼Œiæ˜¯åç§»é‡
            // å¤§å—çš„èµ·å§‹åˆ—ï¼Œå½“å‰çº¿ç¨‹è¦å¤„ç†çš„4ä¸ªæ•°æ®ï¼Œjæ˜¯åç§»é‡
            // æ³¨æ„è¾¹ç•Œæ£€æŸ¥ï¼Œå› ä¸ºçŸ©é˜µç»´åº¦ä¸ä¸€å®šæ˜¯64çš„å€æ•°
           int c_row = blockRow + ty * 4 + i;
           int c_col = blockCol + tx * 4 + j;

           if(c_row < M && c_col < N) {
                C[c_row * N + c_col] = sum[i][j];
           }
        }
    }
}
void initMatrixRandom(float *mat, int rows, int cols) {
    for (int i = 0; i < rows * cols; i++) {
        mat[i] = (float)rand() / RAND_MAX;  // 0-1ä¹‹é—´çš„éšæœºæ•°
    }
}
// éªŒè¯ç»“æœ
bool verifyResult(float* C_cpu,float* C_gpu,int M,int N){
    const float epsilon = 1e-3;
    int errorCount = 0;
    
    for(int i = 0; i < M * N; ++i) {
        float diff = fabs(C_cpu[i] - C_gpu[i]);
        if(diff > epsilon) {
            errorCount++;

            if(errorCount <= 10) {
                printf("é”™è¯¯[%d]: CPU = %.6f, GPU = %.6f, diff = %.6f\n",
                i,C_cpu[i],C_gpu[i],diff);
            }
        }
    }
    if(errorCount > 0) {
        printf("å‘ç° %d ä¸ªé”™è¯¯ (æ€»å…± %d ä¸ªå…ƒç´ )\n",errorCount, M * N);
        return false;
    }
    return true;
}
int main() {

    printf("lesson 4: ç»Ÿä¸€å†…å­˜ç®¡ç†æ±‚çŸ©é˜µä¹˜æ³•\n");

    // 1.generate dta;
    int M = 1024;
    int K = 1024;
    int N = 1024;
    
    printf("çŸ©é˜µç»´åº¦ï¼š\n");
    printf("    A: %d x %d\n",M,K);
    printf("    B: %d x %d\n",K,N);
    printf("    C: %d x %d\n",M,N);

    size_t size_A = M * K * sizeof(float);
    size_t size_B = K * N * sizeof(float);
    size_t size_C = M * N * sizeof(float);

    printf("å†…å­˜å ç”¨:\n");
    printf("    A:%.2f MB\n",size_A / 1024.0f / 1024.0f);
    printf("    B:%.2f MB\n",size_B / 1024.0f / 1024.0f);
    printf("    C:%.2f MB\n",size_C / 1024.0f / 1024.0f);
    printf("    æ€»è®¡:%.2f MB\n",(size_A + size_B + size_C)/ 1024.0f / 1024.0f);

    // åˆ†é…Hostå†…å­˜
    float *h_A = (float*)malloc(size_A);
    float *h_B = (float*)malloc(size_B);
    float *h_C_cpu = (float*)malloc(size_C);
    float *h_C_gpu = (float*)malloc(size_C);
    float *h_C_gpu_tiled = (float*)malloc(size_C);
     float *h_C_gpu_tiled4 = (float*)malloc(size_C);
    if(!h_A || !h_B || !h_C_cpu || !h_C_gpu || !h_C_gpu_tiled|| !h_C_gpu_tiled4) {
        fprintf(stderr,"Host å†…å­˜åˆ†é…å¤±è´¥!\n");
        exit(1);
    }

    // åˆ†é…Deviceå†…å­˜
    float *d_A ,*d_B,*d_C_gpu,*d_C_gpu_tiled,*d_C_gpu_tiled4;
    CUDA_CHECK(cudaMalloc(&d_A,size_A));
    CUDA_CHECK(cudaMalloc(&d_B,size_B));
    CUDA_CHECK(cudaMalloc(&d_C_gpu_tiled,size_C));
    CUDA_CHECK(cudaMalloc(&d_C_gpu,size_C));
    CUDA_CHECK(cudaMalloc(&d_C_gpu_tiled4,size_C));
    // åˆå§‹åŒ–æ•°æ® ï¼ˆåœ¨ Host ä¸Š)
    printf("åˆå§‹åŒ–æ•°æ® ï¼ˆåœ¨ Host ä¸Š)...\n");
    srand(time(NULL));
    initMatrixRandom(h_A,M,K);
    initMatrixRandom(h_B,K,N);

    printf("æ‹·è´æ•°æ®åˆ° Device\n");
    CUDA_CHECK(cudaMemcpy(d_A,h_A,size_A,cudaMemcpyHostToDevice));
    CUDA_CHECK(cudaMemcpy(d_B,h_B,size_B,cudaMemcpyHostToDevice));




    clock_t cpu_start = clock();
    matmulCPU(h_A, h_B, h_C_cpu, M, N, K);
    clock_t cpu_end = clock();
    double cpu_time = (double)(cpu_end - cpu_start) / CLOCKS_PER_SEC * 1000;

    // è®¡ç®—GLOPS
    // çŸ©é˜µä¹˜æ³•çš„æµ®ç‚¹æ“ä½œæ•°ï¼šæ¯ä¸ªè¾“å‡ºå…ƒç´ éœ€è¦Kæ¬¡ä¹˜æ³•+Kæ¬¡åŠ æ³• = 2Kæ¬¡æ“ä½œ
    // æ€»æ“ä½œæ•°ï¼š M * N * 2K

    double gflops_cpu = (2.0 * M * N * K) / (cpu_time / 1000.0) / 1e9;
    
    printf("CPUå®Œæˆï¼\n");
    printf("  è€—æ—¶: %.2f ms\n", cpu_time);
    printf("  æ€§èƒ½: %.2f GFLOPS\n", gflops_cpu);
    printf("  ç¤ºä¾‹ç»“æœ: C[0][0]=%.2f, C[10][10]=%.2f\n\n",
           h_C_cpu[0], h_C_cpu[10 * N + 10]);
        
    // é…ç½®2Dçº¿ç¨‹å—å’Œç½‘ç»œ
    dim3 blockDim(16,16);
    dim3 gridDim((N + blockDim.x - 1) / blockDim.x,(M + blockDim.y - 1) / blockDim.y);
    printf("Kernelé…ç½®ï¼š\n");
    printf("    Block: (%d,%d) = %d thread\n",blockDim.x,blockDim.y,blockDim.x * blockDim.y);
    printf("    Grid: (%d,%d) = %d block\n",gridDim.x,gridDim.y,gridDim.x * gridDim.y);
               

    // =======================================================
    // ğŸš€ã€ä¿®æ­£ç‰ˆã€‘WSL2 ä¸“ç”¨é¢„çƒ­æ–¹æ¡ˆ
    // =======================================================
    int deviceId = 0;
    cudaGetDevice(&deviceId);
    printf("Debug: Current Device ID = %d (WSL2 ç¯å¢ƒå¿½ç•¥ Prefetch)\n", deviceId);

    printf("æ­£åœ¨é¢„çƒ­ GPU... (åˆ©ç”¨ Kernel è§¦å‘ç¼ºé¡µä¸­æ–­æ¬è¿æ•°æ®)\n");

    // 1. ã€å…³é”®ã€‘ä¸è¦ç”¨ cudaMemPrefetchAsyncï¼Œç›´æ¥è·‘ä¸€æ¬¡ Kernel
    // å½“ GPU è¯•å›¾è¯»å– data[i] æ—¶ï¼Œé©±åŠ¨ä¼šè‡ªåŠ¨æŠŠæ•°æ®ä» CPU æ¬åˆ° GPU L2/æ˜¾å­˜
    // è¿™æ¬¡è¿è¡Œä¼šå¾ˆæ…¢ï¼ˆå› ä¸ºåŒ…å«æ¬è¿æ—¶é—´ï¼‰ï¼Œæˆ‘ä»¬ä¸è®¡å…¥æˆç»©
    matmul_native<<<gridDim,blockDim>>>(d_A,d_B,d_C_gpu,M,N,K);
    
    // 2. å¿…é¡»åŒæ­¥ï¼ç­‰å¾…æ¬è¿å’Œç¬¬ä¸€æ¬¡è®¡ç®—å½»åº•ç»“æŸ
    CUDA_CHECK(cudaDeviceSynchronize());
    
    printf("é¢„çƒ­å®Œæˆï¼æ•°æ®å·²åœ¨æ˜¾å­˜ä¸­ï¼Œå¼€å§‹çœŸæ­£çš„æ€§èƒ½æµ‹è¯•...\n\n");
    // =======================================================

    printf("========================================\n");
    printf("GPUè®¡ç®—ï¼ˆæœ´ç´ å®ç°ï¼‰\n");
    printf("========================================\n");

    cudaEvent_t start1,stop1;
    CUDA_CHECK(cudaEventCreate(&start1));
    CUDA_CHECK(cudaEventCreate(&stop1));

    cudaEventRecord(start1);

    matmul_native<<<gridDim,blockDim>>>(d_A,d_B,d_C_gpu,M,N,K);

    CUDA_CHECK(cudaEventRecord(stop1));

    cudaEventSynchronize(stop1);

    float gpu_time;
    CUDA_CHECK(cudaEventElapsedTime(&gpu_time,start1,stop1));
    cudaDeviceSynchronize();
    // printf("GPU ç»“æœ: %d (è€—æ—¶: %.2f ms)\n\n", *gpu_result, gpu_time);
    
    double gflops_gpu_naive = (2.0 * M * N * K) / (gpu_time / 1000.0) / 1e9;
    // æ‹·è´ç»“æœä¼šHost
    CUDA_CHECK(cudaMemcpy(h_C_gpu,d_C_gpu,size_C,cudaMemcpyDeviceToHost));
    printf("GPU-naiveå®Œæˆï¼\n");
    printf("  è€—æ—¶: %.2f ms\n", gpu_time);
    printf("  æ€§èƒ½: %.2f GFLOPS\n", gflops_gpu_naive);
    printf("  ç¤ºä¾‹ç»“æœ: C[0][0]=%.2f, C[10][10]=%.2f\n\n",
           h_C_gpu[0], h_C_gpu[10 * N + 10]);
   
    printf("========================================\n");
    printf("éªŒè¯ç»“æœ...\n");
    if (verifyResult(h_C_cpu, h_C_gpu, M, N)) {
        printf("âœ“ ç»“æœæ­£ç¡®ï¼GPUè®¡ç®—ä¸CPUä¸€è‡´\n");
    } else {
        printf("âœ— ç»“æœé”™è¯¯ï¼è¯·æ£€æŸ¥ä»£ç \n");
    }
    
    printf("========================================\n");
    printf("GPUè®¡ç®—ï¼ˆå…±äº«å†…å­˜å’Œåˆ†å—çŸ©é˜µï¼‰\n");
    printf("========================================\n");


    cudaEvent_t start2,stop2;
    CUDA_CHECK(cudaEventCreate(&start2));
    CUDA_CHECK(cudaEventCreate(&stop2));

    cudaEventRecord(start2);

    matmalGPU_tiled<<<gridDim,blockDim>>>(d_A,d_B,d_C_gpu_tiled,M,N,K);

    CUDA_CHECK(cudaEventRecord(stop2));

    cudaEventSynchronize(stop2);

    float gpu_time_tiled;
    CUDA_CHECK(cudaEventElapsedTime(&gpu_time_tiled,start2,stop2));
    cudaDeviceSynchronize();
    
    double gflops_gpu_tiled = (2.0 * M * N * K) / (gpu_time_tiled / 1000.0) / 1e9;
    // æ‹·è´ç»“æœä¼šHost
    CUDA_CHECK(cudaMemcpy(h_C_gpu_tiled,d_C_gpu_tiled,size_C,cudaMemcpyDeviceToHost));
    printf("GPU-tiledå®Œæˆï¼\n");
    printf("  è€—æ—¶: %.2f ms\n", gpu_time_tiled);
    printf("  æ€§èƒ½: %.2f GFLOPS\n", gflops_gpu_tiled);
    printf("  ç¤ºä¾‹ç»“æœ: C[0][0]=%.2f, C[10][10]=%.2f\n\n",
           h_C_gpu_tiled[0], h_C_gpu_tiled[10 * N + 10]);
   
    printf("========================================\n");
    printf("éªŒè¯ç»“æœ...\n");
    if (verifyResult(h_C_cpu, h_C_gpu_tiled, M, N)) {
        printf("âœ“ ç»“æœæ­£ç¡®ï¼GPUè®¡ç®—ä¸CPUä¸€è‡´\n");
    } else {
        printf("âœ— ç»“æœé”™è¯¯ï¼è¯·æ£€æŸ¥ä»£ç \n");
    }

    printf("========================================\n");
    printf("GPUè®¡ç®—ï¼ˆå…±äº«å†…å­˜å’Œåˆ†å—çŸ©é˜µï¼ˆ4 * 4ï¼‰\n");
    printf("========================================\n");

    dim3 blockDim_cg(16, 16); // æ¯ä¸ªçº¿ç¨‹å¤„ç† 4*4 å°å—ï¼Œæ€»å…±å°±æ˜¯ 64*64ä¸ªå°å—
    dim3 gridDim_cg((N + 64 - 1) / 64,
                 (M + 64 - 1) / 64);
    cudaEvent_t start3,stop3;
    CUDA_CHECK(cudaEventCreate(&start3));
    CUDA_CHECK(cudaEventCreate(&stop3));

    cudaEventRecord(start3);

    matmalGPU_tiled4<<<gridDim_cg,blockDim_cg>>>(d_A,d_B,d_C_gpu_tiled4,M,N,K);

    CUDA_CHECK(cudaEventRecord(stop3));

    cudaEventSynchronize(stop3);

    float gpu_time_tiled4;
    CUDA_CHECK(cudaEventElapsedTime(&gpu_time_tiled4,start3,stop3));
    cudaDeviceSynchronize();
    
    double gflops_gpu_tiled4 = (2.0 * M * N * K) / (gpu_time_tiled4 / 1000.0) / 1e9;
    // æ‹·è´ç»“æœä¼šHost
    CUDA_CHECK(cudaMemcpy(h_C_gpu_tiled4,d_C_gpu_tiled4,size_C,cudaMemcpyDeviceToHost));
    printf("GPU-tiledå®Œæˆï¼\n");
    printf("  è€—æ—¶: %.2f ms\n", gpu_time_tiled4);
    printf("  æ€§èƒ½: %.2f GFLOPS\n", gflops_gpu_tiled4);
    printf("  ç¤ºä¾‹ç»“æœ: C[0][0]=%.2f, C[10][10]=%.2f\n\n",
           h_C_gpu_tiled4[0], h_C_gpu_tiled4[10 * N + 10]);
   
    printf("========================================\n");
    printf("éªŒè¯ç»“æœ...\n");
    if (verifyResult(h_C_cpu, h_C_gpu_tiled4, M, N)) {
        printf("âœ“ ç»“æœæ­£ç¡®ï¼GPUè®¡ç®—ä¸CPUä¸€è‡´\n");
    } else {
        printf("âœ— ç»“æœé”™è¯¯ï¼è¯·æ£€æŸ¥ä»£ç \n");
    }
    // å†…å­˜éœ€è¦é‡Šæ”¾æ‰
    free(h_A);
    free(h_B);
    free(h_C_cpu);
    free(h_C_gpu);
    free(h_C_gpu_tiled);
    free(h_C_gpu_tiled4);
    CUDA_CHECK(cudaFree(d_A));
    CUDA_CHECK(cudaFree(d_B));
    CUDA_CHECK(cudaFree(d_C_gpu));
    CUDA_CHECK(cudaFree(d_C_gpu_tiled));
    CUDA_CHECK(cudaFree(d_C_gpu_tiled4));
    CUDA_CHECK(cudaEventDestroy(start1));
    CUDA_CHECK(cudaEventDestroy(stop1));
    CUDA_CHECK(cudaEventDestroy(start2));
    CUDA_CHECK(cudaEventDestroy(stop2));
    CUDA_CHECK(cudaEventDestroy(start3));
    CUDA_CHECK(cudaEventDestroy(stop3));
    
}